# 3. 基本構文

## 3.1 変数

「variable」を使うと変数が定義できる

```tf
variable "example_instance_type" {
  default = "t3.micro"
}
resource "aws_instance" "example" {
  ami           = "ami-0c3fd0f5d33134a76"
  instance_type = var.example_instance_type

  user_data = <<EOF
    #!/bin/bash
    yum install -y httpd
    systemctl start httpd.service
  EOF

  tags = {
    "Name" = "example"
  }
}

```

変数は実行時の`-var`オプションや、環境変数でも上書きできます。

```bash
terraform plan -var "example_instance_type=t3.nano"
```

```bash
TF_VAR_example_instance_type=t3.nano terraform plan
```

## 3.2 ローカル変数

「locales」を使うとローカル変数を使える。

```tf
locals {
  example_instance_type = "t3.nano"
}
resource "aws_instance" "example" {
  ami           = "ami-0c3fd0f5d33134a76"
  instance_type = local.example_instance_type

  user_data = <<EOF
    #!/bin/bash
    yum install -y httpd
    systemctl start httpd.service
  EOF

  tags = {
    "Name" = "example"
  }
}
```

## 3.3 出力値

「output」を使うと出力値を定義できる。

```tf
# リソースに定義したexampleのインスタンスIDを出力する
output "example_instance_id" {
  value = aws_instance.example.id
}

```

## 3.4 データソース

データソースを使うと外部データを参照できる。
例えば最新の Amazon Linux2 の AMI を参照できたりする。

```tf
data "aws_ami" "recent_amazon_linux_2" {
  most_recent = true
  owners      = ["amazon"]

  filter {
    name   = "name"
    values = ["amzn2-ami-hvm-2.0.????????-x86_64-gp2"]
  }

  filter {
    name   = "state"
    values = ["available"]
  }
}
resource "aws_instance" "example" {
  ami           = data.aws_ami.recent_amazon_linux_2.image_id
  instance_type = local.example_instance_type

  user_data = <<EOF
    #!/bin/bash
    yum install -y httpd
    systemctl start httpd.service
  EOF

  tags = {
    "Name" = "example"
  }
}
```

## 3.5 プロバイダ

`provider`を指定すると、AWS や GCP、Azure などに向けることができる。

```tf
provider "aws" {
  region  = "ap-northeast-1"
}
```

## 3.6 参照

セキュリティグループを作る。

```tf
resource "aws_security_group" "example_ec2" {
  name = "example-ec2"

  ingress {
    cidr_blocks = ["0.0.0.0/0"]
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
  }

  egress {
    cidr_blocks = ["0.0.0.0/0"]
    from_port   = 0
    protocol    = "-1"
    to_port     = 0
  }
}
```
